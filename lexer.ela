import fmt::{
  format,
  printlnf,
  FormatOptions,
};

import map::Map;

import fs::File;

// line, column, filename,
alias SourceLocation :: (String, u32, u32);

impl SourceLocation {
  to_string :: fn(self) -> String {
    return format("%:%:%", self, FormatOptions::default());
  }
}
LexError :: struct {
  location: SourceLocation,
  message:  String,
}

TType :: enum {
  Eof,

  Add,
  Sub,
  Div,
  Mul,
  Assign,
  Not,
  Comma,
  
  OpenParen,
  CloseParen,
  
  OpenCurly,
  CloseCurly,

  OpenBracket,
  CloseBracket,

  Semi,

  LT,
  LTE,
  GT,
  GTE,
  EQ,
  NEQ,
  Dot,

  Number,
  String,
  Identifier,

  If,
  Else,
  For,
  In,
  While,
  Make,
  
  Return,
  Continue,
  Break,
  
  Fn,
  Var,

  True,
  False,
}

Token :: struct {
  type: TType,
  value: Option!<String>,
  location: SourceLocation,
}

Lexer :: struct {
  input: String,
  location: SourceLocation,
  buffer: Token[],
  position: u32,

  keywords: Map!<str, TType>,
}

impl Lexer {
  new :: fn(path: String) -> #self {
    mut keywords: Map!<str, TType>;

    keywords.insert("if", TType::If);
    keywords.insert("else", TType::Else);

    keywords.insert("fn", TType::Fn);
    
    keywords.insert("var", TType::Var);

    keywords.insert("while", TType::While);
    keywords.insert("for", TType::For);
    keywords.insert("in", TType::In);
    keywords.insert("make", TType::Make);
    keywords.insert("return", TType::Return);
    keywords.insert("continue", TType::Continue);
    keywords.insert("break", TType::Break);

    keywords.insert("true", TType::True);
    keywords.insert("false", TType::False);

    return .{
      input: File::read_all(path.as_str()).unwrap(),
      location: (path, 0, 0),
      keywords: keywords,
    };
  }

  advance :: fn(*mut self, new_line: bool) {
    if new_line {
      self.location.1++;
      self.location.2 = 1;
    } else {
      self.location.2++;
    }
    self.position++;
  }

  get_next_char :: fn(*mut self, ch: *mut u8) {
    self.advance(false);
    *ch = self.input[self.position];
  }

  get_token :: fn(*mut self) -> Result!<Token, LexError> {
    alias Result :: Result!<Token, LexError>;
    mut token: Token;

    token.type = TType::Eof;

    while self.position < self.input.length {
      mut ch := self.input[self.position];

      if ch == '\n' {
        self.advance(true);
        continue;
      }

      if ch == ' ' || ch == '\t' {
        self.advance(false);
        continue;
      }

      if ch == '/' {
        self.get_next_char(&mut ch);
        if ch == '/' {
          while ch != '\n' && self.position < self.input.length {
            self.get_next_char(&mut ch);
          }
          continue;
        } else if ch == '*' {
          while self.position < self.input.length {
            self.get_next_char(&mut ch);
            if ch == '*' {
              self.get_next_char(&mut ch);
              if ch == '/' {
                break;
              }
            }
          }
          continue;
        } else {
          self.position--;
          ch = '/';
        }
      }

      if ch == '\'' || ch == '\"' {
        quote_char := ch;
        start := self.position;
        self.get_next_char(&mut ch);
        while ch != quote_char && self.position < self.input.length {
          self.get_next_char(&mut ch);
        }
        end := self.position;
        length := end - start - 1;

        token.value = Some(String.{
          data: strndup(self.input.data + start + 1, length),
          length: length,
          capacity: length,
        });

        token.type = TType::String;
        self.advance(false);
        break;
      }

      if ch == '_' || isalpha(ch) {
        start := self.position;
        while ch == '_' || isalnum(ch) || ch == ':' {
          self.get_next_char(&mut ch);
        }
        end := self.position;

        length := end - start;

        token.value = Some(String.{
          data: strndup(self.input.data + start, length),
          length: length,
          capacity: length,
        });

        token.type = TType::Identifier;

        mut value := token.value.unwrap();

        keyword := self.keywords.get(value.as_str());

        // TODO: we can deinit the string here, but we could just avoid allocating it all together.
        if keyword.is_some() {
          token.type = keyword.unwrap();
          value.deinit();
          token.value = None();
        }

        break;
      } else if isdigit(ch) {
        start := self.position;
        while isdigit(ch) {
          self.get_next_char(&mut ch);
        }
        end := self.position;
        length := end - start;

        token.value = Some(String.{
          data: strndup(self.input.data + start, length),
          length: length,
          capacity: length,
        });

        token.type = TType::Number;
        break;
      } else {
        type := switch ch {
          '+' => TType::Add,
          '-' => TType::Sub,
          '/' => TType::Div,
          '*' => TType::Mul,
          '=' => TType::Assign,
          '!' => TType::Not,
          ',' => TType::Comma,
          '.' => TType::Dot,
          '(' => TType::OpenParen,
          ')' => TType::CloseParen,
          '{' => TType::OpenCurly,
          '}' => TType::CloseCurly,
          '[' => TType::OpenBracket,
          ']' => TType::CloseBracket,
          ';' => TType::Semi,
          else => TType::Eof,
        };

        if type != TType::Eof {
          self.advance(false);
          token.type = type;
          break;
        }


        start := self.position;
        while ispunct(ch) {
          self.get_next_char(&mut ch);
        }
        end := self.position;
        length := end - start;

        mut value := String.{
          data: strndup(self.input.data + start, length),
          length: length,
          capacity: length,
        };

        token.type = switch value.as_str() {
          "=" => TType::Assign,
          "<" => TType::LT,
          ">" => TType::GT,
          ">=" => TType::GTE,
          "<=" => TType::LTE,
          "==" => TType::EQ,
          "!=" => TType::NEQ,
        };

        value.deinit();

        break;
      }
    }

    return Result::Ok(token);
  }
}

impl Lexer {
  fill_buffer :: fn(*mut self) {
    while self.buffer.length < 8 {
      token := self.get_token();
      if token.is_err() {
        break;
      }
      self.buffer.push(token.unwrap());
    }
  }

  eat :: fn(*mut self) -> Result!<Token, LexError> {
    if self.buffer.length == 0 {
      self.fill_buffer();
    }
    return Result!<Token, LexError>::Ok(self.buffer.pop_front());
  }

  peek :: fn(*mut self) -> Token {
    if self.buffer.length == 0 {
      self.fill_buffer();
    }
    return self.buffer[0];
  }

  expect :: fn(*mut self, expected: TType) -> Result!<Token, LexError> {
    token_result := self.eat();

    if (token_result.is_err()) {
      return token_result.propagate();
    }

    token := token_result.unwrap();

    if token.type != expected {
      return Result!<Token, LexError>::Err(LexError.{
        location: token.location,
        message: format("Expected token type: %, but got: %", (expected, token.type), FormatOptions::default()),
      });
    }
    return Result!<Token, LexError>::Ok(token);
  }
}