import parser::*;

impl!<T> T[] {
  reverse :: fn(*mut self) {
    len := self.length;

    if len <= 1 then return;

    for i in 0..(len / 2) {
      high := len - 1 - i;
      low := i;
      temp := self.data[low];
      self.data[low] = self.data[high];
      self.data[high] = temp;
    }
  }
}

Interp :: struct {
  scope: *mut Scope,
  nodes: List!<*mut Node>,
}

ControlFlow :: enum {
  None,
  Continue,
  Break,
  Return,
}

StatementResult :: struct {
  control_flow: ControlFlow,
  value: Option!<Rc!<Value>>,
}

impl Interp {
  new :: fn(mut nodes: List!<*mut Node>) -> #self {
    return .{
      scope: new(Scope.{}),
      nodes: nodes,
    };
  }

  visit_if :: fn(*mut self, node: *mut Node) -> StatementResult {
    condition := *self.visit_expression(node.if_node.condition);
    if condition.is_truthy() {
      return self.visit_statement(node.if_node.block);
    } else if node.if_node.else_node.is_some() {
      return self.visit_statement(node.if_node.else_node.unwrap());
    }
    return .{
      control_flow: ControlFlow::None,
      value: None(),
    };
  }

  visit_else :: fn(*mut self, node: *mut Node) -> StatementResult {
    if node.else_node.if_node.is_some() {
      return self.visit_if(node.else_node.if_node.unwrap());
    } else if node.else_node.block.is_some() {
      return self.visit_statement(node.else_node.block.unwrap());
    }
    return .{
      control_flow: ControlFlow::None,
      value: None(),
    };
  }

  visit_while :: fn(*mut self, node: *mut Node) -> StatementResult {
    while {
      if node.while_node.condition.is_some() {
        condition := *self.visit_expression(node.while_node.condition.unwrap());
        if !condition.is_truthy() {
          break;
        }
      }
      result := self.visit_statement(node.while_node.block);
      if result.control_flow == ControlFlow::Break {
        break;
      } else if result.control_flow == ControlFlow::Return {
        return result;
      }
    }
    return .{
      control_flow: ControlFlow::None,
      value: None(),
    };
  }

  visit_for :: fn(*mut self, node: *mut Node) -> StatementResult {
    target := *self.visit_expression(node.for_node.target);
    if target.tag != ValueTag::Array {
      panic("for loop target must be an array");
      return .{};
    }

    for element in target.array {
      self.scope.insert(node.for_node.identifier, element);
      result := self.visit_statement(node.for_node.block);
      if result.control_flow == ControlFlow::Break {
        break;
      } else if result.control_flow == ControlFlow::Return {
        return result;
      }
    }
    return .{
      control_flow: ControlFlow::None,
      value: None(),
    };
  }

  visit_binary :: fn(*mut self, node: *mut Node) -> Rc!<Value> {
    switch node.binary.operator {
      TType::Add: {
        return self.visit_expression(node.binary.left).add(
          self.visit_expression(node.binary.right),
        );
      }
      TType::Sub: {
        return self.visit_expression(node.binary.left).sub(
          self.visit_expression(node.binary.right),
        );
      }
      TType::Mul: {
        return self.visit_expression(node.binary.left).mul(
          self.visit_expression(node.binary.right),
        );
      }
      TType::Div: {
        return self.visit_expression(node.binary.left).div(
          self.visit_expression(node.binary.right),
        );
      }
      TType::LT: {
        return self.visit_expression(node.binary.left).lt(
          self.visit_expression(node.binary.right)
        );
      }
      TType::LTE: {
        return self.visit_expression(node.binary.left).lte(
          self.visit_expression(node.binary.right)
        );
      }
      TType::GT: {
        return self.visit_expression(node.binary.left).gt(
          self.visit_expression(node.binary.right),
        );
      }
      TType::GTE: {
        return self.visit_expression(node.binary.left).gte(
          self.visit_expression(node.binary.right)
        );
      }
      TType::EQ: {
        return self.visit_expression(node.binary.left).eq(
          self.visit_expression(node.binary.right)
        );
      }
      TType::NEQ: {
        return self.visit_expression(node.binary.left).neq(
          self.visit_expression(node.binary.right),
        );
      }
      TType::Assign: {
        mut left := self.visit_expression(node.binary.left);
        right := *self.visit_expression(node.binary.right);
        *left.ptr = right;
      }
    }
    return Value::None();
  }

  visit_unary :: fn(*mut self, node: *mut Node) -> Rc!<Value> {
    operand := *self.visit_expression(node.unary.operand);
    switch node.unary.operator {
      TType::Not: {
        return Rc!<Value>::new(Value.{
          tag: ValueTag::Boolean,
          boolean: !operand.is_truthy(),
        });
      }
      TType::Sub: {
        if operand.tag != ValueTag::Number {
          panic("unary minus can only be applied to numbers");
          return .{};
        }
        return Rc!<Value>::new(Value.{
          tag: ValueTag::Number,
          number: -operand.number,
        });
      }
    }
    return Value::None();
  }

  visit_call :: fn(*mut self, node: *mut Node) -> Rc!<Value> {
    function := *self.visit_expression(node.call.callee);
    mut args: List!<Rc!<Value>>;
    for arg in node.call.arguments {
      value := self.visit_expression(arg);
      args.push(value);
    }

    if function.tag == ValueTag::NativeFunction {
      return function.native_function(args);
    } else if function.tag == ValueTag::Function {
      return self.visit_function(function.function, args);
    } else {
      panic("cannot call a non function");
      return .{};
    }
  }

  visit_member_access :: fn(*mut self, node: *mut Node) -> Rc!<Value> {
    object := *self.visit_expression(node.member_access.base);
    if object.tag != ValueTag::Object {
      panic("cannot access member of a non-object");
      return .{};
    }

    member := node.member_access.member;
    value := object.object.get(member);
    if value.is_none() {
      panic("member not found in object");
      return .{};
    }

    return value.unwrap().clone();
  }

  visit_subscript :: fn(*mut self, node: *mut Node) -> Rc!<Value> {
    array := self.visit_expression(node.subscript.base).ptr;
    if array.tag != ValueTag::Array {
      panic("cannot subscript a non-array");
      return .{};
    }

    index := *self.visit_expression(node.subscript.index);
    if index.tag != ValueTag::Number {
      panic("array index must be a number");
      return .{};
    }

    idx := index.number as u64;
    if idx >= array.array.length {
      panic("array index out of bounds");
      return .{};
    }

    return array.array[idx].clone();
  }

  visit_object_literal :: fn(*mut self, node: *mut Node) -> Rc!<Value> {
    mut object: Map!<String, Rc!<Value>>;

    for key, value_node in node.object_literal.key_values {
      value := self.visit_expression(value_node);
      object.insert(key, value);
    }

    return Rc!<Value>::new(Value.{
      tag: ValueTag::Object,
      object: object,
    });
  }

  visit_array_literal :: fn(*mut self, node: *mut Node) -> Rc!<Value> {
    mut array: List!<Rc!<Value>>;

    for element_node in node.array_literal.values {
      value := self.visit_expression(element_node);
      array.push(value);
    }

    return Rc!<Value>::new(Value.{
      tag: ValueTag::Array,
      array: array,
    });
  }

  visit_block :: fn(*mut self, node: *mut Node) -> StatementResult {
    block := &mut node.block;
    mut cf: ControlFlow = ControlFlow::None;

    old_scope := self.scope;
    self.scope = block.scope;
    defer self.scope = old_scope;

    for statement in node.block.statements {
      result := self.visit_statement(statement);

      if result.control_flow == ControlFlow::Return
        then return result;
    }

    return .{
      control_flow: ControlFlow::None,
      value: None(),
    };
  }

  visit_variable :: fn(*mut self, node: *mut Node) -> StatementResult {
    variable_name := node.variable.name;

    if node.variable.value.is_some() {
      initial_value := self.visit_expression(node.variable.value.unwrap());
      self.scope.insert(variable_name, initial_value);
    } else {
      self.scope.insert(variable_name, Value::None());
    }

    return .{
      control_flow: ControlFlow::None,
      value: None(),
    };
  }

  visit_function :: fn(*mut self, node: *mut Node, args: List!<Rc!<Value>>) -> Rc!<Value> {
    function : *mut Function = &mut node.function;
    mut i := 0;
    for param in function.parameters {
      if i >= args.length {
        break;
      }
      function.block.block.scope.insert(param, args[i]);
      i++;
    }

    _, value := self.visit_block(function.block);

    if value.is_none() {
      return Value::None();
    } else {
      return value.unwrap();
    }
  }

  visit_function_header :: fn(*mut self, node: *mut Node) {
    funcy := &mut node.function;

    self.scope.insert(funcy.name, Rc!<Value>::new(Value.{
      tag: ValueTag::Function,
      function: &mut node.function,
    }));

    old_scope := self.scope;
    funcy.block.block.scope = old_scope.create_child();
    self.scope = funcy.block.block.scope;

    old_nodes := self.nodes;
    self.nodes = funcy.block.block.statements;

    defer {
      self.nodes = old_nodes;
      self.scope = old_scope;
    }

    for param in funcy.parameters {
      self.scope.insert(param, Value::None());
    }
  }

  visit_expression :: fn(*mut self, node: *mut Node) -> Rc!<Value> {
    switch node.tag {
      NodeTag::Boolean: {
        return Rc!<Value>::new(Value.{
          tag: ValueTag::Boolean,
          boolean: node.boolean,
        });
      }
      NodeTag::None: {
        return Rc!<Value>::new(Value.{
          tag: ValueTag::None,
        });
      }
      NodeTag::Number: {
        return Rc!<Value>::new(Value.{
          tag: ValueTag::Number,
          number: node.number,
        });
      }
      NodeTag::String: {
        return Rc!<Value>::new(Value.{
          tag: ValueTag::String,
          string: node.string,
        });
      }
      NodeTag::Identifier: {
        variable := self.scope.lookup(node.identifier);
        if variable.is_none() {
          printlnf("use of undeclared variable: %", (node.identifier,));
          panic("");
        }
        return variable.unwrap().clone();
      }
      NodeTag::Binary: {
        return self.visit_binary(node);
      }
      NodeTag::Unary: {
        return self.visit_unary(node);
      }
      NodeTag::MemberAccess: {
        return self.visit_member_access(node);
      }
      NodeTag::Subscript: {
        return self.visit_subscript(node);
      }
      NodeTag::ObjectLiteral: {
        return self.visit_object_literal(node);
      }
      NodeTag::ArrayLiteral: {
        return self.visit_array_literal(node);
      }
      NodeTag::Call: {
        return self.visit_call(node);
      }
    }
    return Value::None();
  }

  visit_statement :: fn(*mut self, node: *mut Node) -> StatementResult {
    if node.tag == NodeTag::Boolean ||
       node.tag ==  NodeTag::Number ||
       node.tag ==  NodeTag::String ||
       node.tag ==  NodeTag::None ||
       node.tag ==  NodeTag::Identifier ||
       node.tag ==  NodeTag::Binary ||
       node.tag ==  NodeTag::Unary ||
       node.tag ==  NodeTag::MemberAccess ||
       node.tag ==  NodeTag::Subscript ||
       node.tag ==  NodeTag::ObjectLiteral ||
       node.tag ==  NodeTag::ArrayLiteral ||
       node.tag ==  NodeTag::Call {
        return .{
          control_flow: ControlFlow::None,
          value:  Some(self.visit_expression(node)),
        }
    }
    switch node.tag {
      NodeTag::Block: {
        return self.visit_block(node);
      }
      NodeTag::If: {
        return self.visit_if(node);
      }
      NodeTag::Else: {
        return self.visit_else(node);
      }
      NodeTag::While: {
        return self.visit_while(node);
      }
      NodeTag::For: {
        return self.visit_for(node);
      }
      NodeTag::Continue: {
        return .{
          control_flow: ControlFlow::Continue,
        };
      }
      NodeTag::Break: {
        return .{
          control_flow: ControlFlow::Break,
        };
      }
      NodeTag::Return: {
        mut value: Option!<Rc!<Value>>;

        if node.return_node.expression.is_some() {
          value = Some(self.visit_expression(node.return_node.expression.unwrap()));
        }

        return .{
          control_flow: ControlFlow::Return,
          value: value,
        };
      }
      NodeTag::Variable: {
        return self.visit_variable(node);
      }
    }
    panic("invalid node");
    return .{};
  }

  run :: fn(*mut self) -> Value {
    mut visited_nodes: List!<*mut Node>;

    for node in self.nodes {
      if node.tag == NodeTag::Function {
        visited_nodes.push(node);
        self.visit_function_header(node);
      }
    }

    for node in self.nodes {
      if visited_nodes.contains(node, fn(a: *mut Node, b: *mut Node) -> bool {
        return a == b;
      }) then continue;

      self.visit_statement(node);
    }

    return .{};
  }
}